plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'org.jlleitschuh.gradle.ktlint' version '10.2.1'
    id 'io.gitlab.arturbosch.detekt' version '1.20.0'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdkVersion 32
    buildToolsVersion "31.0.0"

    defaultConfig {
        applicationId "com.screenmeet.live"
        minSdkVersion 23
        targetSdkVersion 32
        versionCode 18
        versionName "2.1.0"

        ndk {
            abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        }
        String apiKey = System.getenv("SM_API_KEY");
        buildConfigField("String", "SM_API_KEY", "\"" + apiKey + "\"")
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

detekt {
    config = files("$rootDir/.detekt/config.yml")
}

ktlint {
    verbose = true
    android = true
    outputToConsole = true
    outputColorName = "RED"
    ignoreFailures = false
    enableExperimentalRules = false
    disabledRules = ["no-wildcard-imports", "max-line-length", "import-ordering"]
    filter {
        exclude("**/generated/**")
        include("**/kotlin/**")
    }
}

dependencies {
    implementation "com.screenmeet:sdk:${screenmeetVersion}"
    if (Boolean.parseBoolean(includeFlutter)) implementation project(path: ':flutter_demo')
    if (Boolean.parseBoolean(includeReactNative)) implementation project(path: ':react_demo')

    implementation "androidx.core:core-ktx:1.7.0"
    implementation "androidx.appcompat:appcompat:1.4.1"
    implementation "androidx.activity:activity-ktx:1.4.0"
    implementation "androidx.fragment:fragment-ktx:1.4.1"
    implementation "androidx.navigation:navigation-fragment-ktx:2.4.2"

    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation "androidx.constraintlayout:constraintlayout:2.1.3"

    implementation "androidx.lifecycle:lifecycle-common-java8:2.4.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.4.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.4.1"

    implementation "com.google.android.material:material:1.6.0"
    implementation "com.google.dagger:hilt-android:2.40.1"
    kapt "com.google.dagger:hilt-compiler:2.40.1"
    implementation "androidx.hilt:hilt-navigation:1.0.0"

    implementation "com.otaliastudios:zoomlayout:1.9.0"
    implementation 'com.camerakit:camerakit:1.0.0-beta3.11'
    implementation 'com.camerakit:jpegkit:0.1.0'

    //noinspection GradleDependency
    implementation 'com.github.mukeshsolanki:android-otpview-pinview:2.1.2'

    implementation "dev.chrisbanes.insetter:insetter:0.6.0"

    def appCenterSdkVersion = '4.2.0'
    implementation "com.microsoft.appcenter:appcenter-analytics:${appCenterSdkVersion}"
    implementation "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"
}