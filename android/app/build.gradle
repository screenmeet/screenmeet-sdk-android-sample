plugins {
    id 'kotlin-kapt'
    id 'kotlin-android'
    id 'com.android.application'
    id 'dagger.hilt.android.plugin'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id("org.jlleitschuh.gradle.ktlint") version "11.3.1"
    id("io.gitlab.arturbosch.detekt") version "1.22.0"
}

android {
    namespace "com.screenmeet.live"
    compileSdk 34

    defaultConfig {
        applicationId "com.screenmeet.live"
        minSdkVersion 23
        targetSdkVersion 33
        versionCode 37
        versionName "3.0.12"

        ndk {
            abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        }
        String apiKey = System.getenv("SM_API_KEY")
        buildConfigField("String", "SM_API_KEY", "\"" + apiKey + "\"")

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

detekt {
    config = files("$rootDir/.detekt/config.yml")
}

ktlint {
    verbose.set(true)
    android.set(true)
    outputToConsole.set(true)
    outputColorName.set("RED")
    ignoreFailures.set(false)
    enableExperimentalRules.set(true)
    //noinspection GrDeprecatedAPIUsage
    disabledRules.set(["no-wildcard-imports", "import-ordering"])
    filter {
        exclude("**/generated/**")
        include("**/kotlin/**")
    }
}

dependencies {
    implementation("com.screenmeet:sdk:${screenmeetVersion}")
    if (Boolean.parseBoolean(includeFlutter)) implementation project(path: ':flutter_demo')
    if (Boolean.parseBoolean(includeReactNative)) implementation project(path: ':react_demo')

    implementation('org.jetbrains.kotlin:kotlin-reflect:1.9.0')

    implementation "androidx.core:core-ktx:1.10.1"
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "androidx.activity:activity-ktx:1.7.2"
    implementation "androidx.fragment:fragment-ktx:1.6.1"
    implementation "androidx.navigation:navigation-fragment-ktx:2.7.1"

    implementation 'androidx.recyclerview:recyclerview:1.3.1'
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "androidx.coordinatorlayout:coordinatorlayout:1.2.0"
    implementation("androidx.datastore:datastore-preferences:1.0.0")

    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.6.1"

    implementation "com.google.android.material:material:1.9.0"

    implementation 'com.google.firebase:firebase-crashlytics-ktx:18.4.1'
    implementation 'com.google.firebase:firebase-analytics-ktx:21.3.0'

    kapt "com.google.dagger:hilt-compiler:2.47"
    implementation "com.google.dagger:hilt-android:2.47"
    implementation "androidx.hilt:hilt-navigation:1.0.0"

    implementation "com.otaliastudios:zoomlayout:1.9.0"

    //noinspection GradleDependency
    implementation 'com.github.mukeshsolanki:android-otpview-pinview:2.1.2'

    implementation 'dev.chrisbanes.insetter:insetter:0.6.1'

    testImplementation("junit:junit:4.13.2")
    androidTestImplementation("androidx.test:core-ktx:1.5.0")
    androidTestImplementation("androidx.test.ext:junit:1.1.5")
    implementation("androidx.test.espresso:espresso-core:3.5.1")
    androidTestImplementation("androidx.test.espresso:espresso-core:3.5.1")
}